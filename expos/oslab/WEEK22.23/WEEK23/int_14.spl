//interrupt 14 Semlock,semunlock
alias SystemCallNum R1;
alias userSP R2;
userSP=SP;
alias currentPID R3;
currentPID=[SYSTEM_STATUS_TABLE+1];
SystemCallNum = [([PTBR + 2 * ((userSP - 5)/ 512)]*512)+(userSP - 5) % 512];
[PROCESS_TABLE + ( [SYSTEM_STATUS_TABLE + 1] * 16) + 13] = SP;
SP=[PROCESS_TABLE + [SYSTEM_STATUS_TABLE + 1] * 16 + 11]*512 -1;

alias retval R4;
retval = ([PTBR + 2 * ((userSP - 1) / 512)] * 512) + ((userSP - 1) % 512);

//semlock system call
//print("semen");
if(SystemCallNum==19)then
	//print("s_lock");
	[PROCESS_TABLE + currentPID*16 + 9] = 19;
	alias pprt R5;
	pprt=[PROCESS_TABLE + currentPID*16 +11]*512 +(512-16);
	alias semid R6;
	semid=[([PTBR + 2 * ((userSP - 4)/ 512)]*512)+(userSP - 4) % 512];
	//print(semid);
	alias semti R7;
	if (semid<0 || semid>7 || [pprt+2*semid]!=1)then
		[retval]=-1;
	else
		semti=[pprt+2*semid +1];
		while([SEMAPHORE_TABLE +semti*4 +0]!=-1 &&[SEMAPHORE_TABLE +semti*4 +0]!=currentPID)do
			[PROCESS_TABLE + currentPID*16 +4]=WAIT_SEMAPHORE;
			[PROCESS_TABLE + currentPID*16 +5]=semti;
			multipush(R1,R2,R3,R4,R5,R6,R7);
			call MOD_5;
			multipop(R1,R2,R3,R4,R5,R6,R7);
		endwhile;
		[SEMAPHORE_TABLE+semti*4 +0]=currentPID;
		[retval]=0;
	endif;
endif;

//semUnlock system call

if(SystemCallNum==20)then
	//print("s_un");
	[PROCESS_TABLE + currentPID*16 + 9] = 20;
	alias pprt R5;
	pprt=[PROCESS_TABLE + currentPID*16 +11]*512 +(512-16);
	alias semid R6;
	semid=[([PTBR + 2 * ((userSP - 4)/ 512)]*512)+(userSP - 4) % 512];
	//print(semid);
	alias semti R7;
	if (semid<0 || semid>7 || [pprt+2*semid]!=1)then
		[retval]=-1;
	else
		semti=[pprt+2*semid +1];
		if ([SEMAPHORE_TABLE +semti*4 +0]!=-1)then
			if([SEMAPHORE_TABLE+semti*4 +0]!=currentPID)then
				[retval]=-2;
			else
				[SEMAPHORE_TABLE+semti*4 +0]=-1;
				alias counter R8;
				counter=1;
				while(counter<=15)do
					if([PROCESS_TABLE+counter*16+4]==WAIT_SEMAPHORE && [PROCESS_TABLE+counter*16 +5]==semti)then
						[PROCESS_TABLE+counter*16+4]=READY;
					endif;
					counter=counter+1;
				endwhile;
			endif;
		endif;
	endif;
endif;

SP = [PROCESS_TABLE + ( [SYSTEM_STATUS_TABLE + 1] * 16) + 13];
[PROCESS_TABLE + [SYSTEM_STATUS_TABLE + 1] * 16 + 9] = 0;
//print("semlos");
ireturn;
