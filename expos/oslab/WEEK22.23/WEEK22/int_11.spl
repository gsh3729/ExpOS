//interrupt 11 Routine: Getpid, Getppid, Wait, Signal
alias SystemCallNum R1;
alias userSP R2;
userSP=SP;
alias currentPID R3;
currentPID=[SYSTEM_STATUS_TABLE+1];
SystemCallNum = [([PTBR + 2 * ((userSP - 5)/ 512)]*512)+(userSP - 5) % 512];
[PROCESS_TABLE + ( [SYSTEM_STATUS_TABLE + 1] * 16) + 13] = SP;
SP=[PROCESS_TABLE + [SYSTEM_STATUS_TABLE + 1] * 16 + 11]*512 -1;

alias retval R4;
retval = ([PTBR + 2 * ((userSP - 1) / 512)] * 512) + ((userSP - 1) % 512);

//wait system call

if (SystemCallNum==13) then
	[PROCESS_TABLE + currentPID*16 + 9] = 13;
	alias pid R5;
	pid=[([PTBR + 2 * ((userSP - 4)/ 512)]*512)+(userSP - 4) % 512];
	if (pid==[SYSTEM_STATUS_TABLE + 1] || pid<1 || pid >15 || [PROCESS_TABLE + pid*16 + 4]==TERMINATED ) then
	 	[retval] = -1;
	else
		[PROCESS_TABLE + currentPID*16 +4]=WAIT_PROCESS;
		[PROCESS_TABLE + currentPID*16 +5]=pid;
		multipush(R1,R2,R3,R4,R5);
		call MOD_5;
		multipop(R1,R2,R3,R4,R5);
		[retval]=0;
	endif;
endif;

//signal system call

if (SystemCallNum==14) then
	[PROCESS_TABLE + currentPID*16 + 9] = 14;
	alias counter R5;
	counter=1;
	while(counter<=15)do
		if([PROCESS_TABLE + counter*16 +4]==WAIT_PROCESS && [PROCESS_TABLE +counter*16 +5]==currentPID)then
			[PROCESS_TABLE + counter*16 +4]=READY;
		endif;
		counter= counter+1;
	endwhile;
	[retval]=0;
endif;

//getppid system call

if (SystemCallNum==12) then
	[retval]=[PROCESS_TABLE + currentPID*16 + 2];
endif;

//getpid systemcall

if (SystemCallNum==11) then
	[retval]=currentPID;
endif;

SP = [PROCESS_TABLE + ( [SYSTEM_STATUS_TABLE + 1] * 16) + 13];
[PROCESS_TABLE + [SYSTEM_STATUS_TABLE + 1] * 16 + 9] = 0;
ireturn;
